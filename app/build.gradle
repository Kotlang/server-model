plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.protobuf'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.android.server_model"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            proto {
                srcDir("src/main/auth-model/")
                srcDir("src/main/social-model/")
            }
            java {
                srcDir("src/main/generated/debug/java")
                srcDir("src/main/generated/debug/grpc")
            }
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //proto data store
    implementation  "androidx.datastore:datastore-core:1.0.0-beta02"

    //grpc
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.grpc:grpc-core:1.38.0'
    implementation 'io.grpc:grpc-okhttp:1.38.0'
    implementation 'io.grpc:grpc-stub:1.38.0'
    implementation('io.grpc:grpc-protobuf-lite:1.38.0') {
        exclude module: "protobuf-lite"
    }

    //grpcKt
    implementation "io.grpc:grpc-kotlin-stub:1.1.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0"
    implementation 'androidx.datastore:datastore-rxjava3:1.0.0-beta02'
}
protobuf {
    protoc {
        // You still need protoc like in the non-Android case
        artifact = 'com.google.protobuf:protoc:3.17.3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.38.0'
        }
        java {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
        grpcKt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:1.1.0:jdk7@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                java {
                    option 'lite'
                }
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
                grpcKt {
                    option 'lite'
                }
            }
        }
    }

    generatedFilesBaseDir = "$projectDir/src/main/generated"
}